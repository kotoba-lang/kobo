(ns kobo.grant
  (:require [clojure.set :as set]))

(defn normalize-capabilities [caps]
  (->> caps (remove nil?) (map str) set))

(defn grant
  ([capabilities] (grant capabilities {}))
  ([capabilities attrs]
   (merge {:kobo.grant/capabilities (normalize-capabilities capabilities)
           :kobo.grant/limits {}}
          attrs)))

(defn intersect
  "Return the effective grant shared by all grant-like maps.

  Each input may use :kobo.grant/capabilities or :capabilities."
  [& grants]
  (let [cap-sets (map #(normalize-capabilities
                        (or (:kobo.grant/capabilities %) (:capabilities %)))
                      grants)
        caps (if (seq cap-sets)
               (apply set/intersection cap-sets)
               #{})]
    {:kobo.grant/capabilities caps
     :kobo.grant/source-count (count grants)}))

(defn allowed? [effective required]
  (let [caps (:kobo.grant/capabilities effective #{})
        required (normalize-capabilities required)]
    (empty? (remove caps required))))

(defn explain [effective required]
  (let [caps (:kobo.grant/capabilities effective #{})
        missing (vec (sort (remove caps (normalize-capabilities required))))]
    (if (seq missing)
      {:kobo.grant/allowed? false
       :kobo.grant/reason :missing-capabilities
       :kobo.grant/missing missing}
      {:kobo.grant/allowed? true})))
